cload (DIR^"../../fl_compiler/mk_rom3.fl");

load "InputSort.fl";

forward_declare {llength :: (int list) -> int};
let llength l = 
    l = [] => 0 |
    1+(llength (tl l))
;

forward_declare {firstn :: int -> (int list) -> (int list)};
let firstn cnt l = cnt = 0 => [] | (hd l):(firstn (cnt-1) (tl l));

forward_declare {butfirstn :: int -> (int list) -> (int list)};
let butfirstn cnt l = cnt = 0 => l | (butfirstn (cnt-1) (tl l));

forward_declare {merge :: (int list) -> (int list) -> (int list)};
let merge l1 l2 =
    l1 = [] => l2 |
    l2 = [] => l1 |
    let e1 = hd l1 in
    let e2 = hd l2 in
    e1 > e2 => (e2 : (merge l1 (tl l2))) | (e1 : (merge (tl l1) l2))
;

forward_declare {merge_sort :: (int list) -> (int list)};
let merge_sort l =
    let len = llength l in
    2 > len => l | 
    let half = len/2 in
    merge (merge_sort (firstn half l)) (merge_sort (butfirstn half l))
;


forward_declare {list_last :: (int list) -> int};
let list_last l = 
    let rem = tl l in
    rem = [] => hd l |
    list_last rem
;

let res =
    let l = merge_sort xs in
    let first = hd l in
    let last = list_last l in
    last-first
;

let main = (res = answer);

mk_cephalopode_rom "Sort.rom" "main";

quit;
